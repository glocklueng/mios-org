;; sub_menu_key_plus_menu_7.inc

;; -------------[MENU 7]-----------------------------------------------------------------------------
;; -------------[SET VELOCITY TO TONE ABC + NOISE PERIOD @ MAIN MENU7 - VELOCITY CONTROL]-------------

PLUS_KEY_SUBROUTINE_AT_MAIN_MENU7
;; jump in here from sub_menu_key_plus.inc

	;; 1) CHECK WHICH ITEM IS 
	;; IN EDITING MODE "*"
	;; VIA AY_LCD_COURSER_POS_MAIN_MENU_VELOCITY_CONFIG
	;;
	;; 2) SET ITEM 

	;; --[ 1) CHECK ITEM (ITEM POS)]-------

	;; CHECK LOCATION VIA EDITING MODE "*"
	;; AND JUMP TO THE EDITING MODE ITEM
	movff AY_LCD_COURSER_POS_MAIN_MENU_VELOCITY_CONFIG, MIOS_PARAMETER1
	movf  MIOS_PARAMETER1, W
	JUMPTABLE_4BYTES 5 ;; [ITEM 1 to 4 JUMP, ITEM 0 = NOT VALID EXIT HERE]
		goto LABEL_MAIN_MENUE_SWITCH			;; POS0 - EXIT HERE TO MAIN MENU
		goto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_A	;; POS1 - SET VELOCITY TO TONE A
		goto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_B	;; POS2 - SET VELOCITY TO TONE B
		goto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_C	;; POS3 - SET VELOCITY TO TONE C
		goto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_PERIOD ;; POS4 - SET VELOCITY NOISE PERIOD


LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_A	
	;; ITEM POS1 - SET VELOCITY TO TONE A (MIDI VELOCITY DATA TO VOLUME OF TONE A)
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT0_VELOCITY_ON_OFF_TONE_A
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH


LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_B	
	;; POS2 - SET VELOCITY TO TONE B
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT1_VELOCITY_ON_OFF_TONE_B
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH



LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_C
	;; POS1 - SET VELOCITY TO TONE C
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT2_VELOCITY_ON_OFF_TONE_C
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH


;; ---- [VELOCITY TO NOISE PERIOD - SWITCH VELOCITY NOISE PERIOD TO VELOCITY FROM TONE AB or C]-------

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_PERIOD
	;; CHECK IF BIT3 is SET
	btfss AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT3_VELOCITY_TO_NOISE_PERIOD_ON_OFF
		;; NO, BIT3 IS NOT SET
		;; SO SET BIT3 AND BIT5 => SET VELOCITY = TONE A  
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_A
	
		;; YES - BIT3 IS SET - CHECK IF VELOCITY IS TONE A
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_CHK_VELOCITY_NOISE_CH_A

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_A
	;; SET VELOCITY TONE A TO NOISE PERIOD
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT3_VELOCITY_TO_NOISE_PERIOD_ON_OFF  ;; VELOCITY TO NOISE RATE ON
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT5_VELOCITY_A_TO_NOISE_PERIOD_ON_OFF;; Take VELOCITY from A
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT6_VELOCITY_B_TO_NOISE_PERIOD_ON_OFF
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT7_VELOCITY_C_TO_NOISE_PERIOD_ON_OFF
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_CHK_VELOCITY_NOISE_CH_A
	;; IS BIT5 [TONE A] = SET?
	btfss AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT5_VELOCITY_A_TO_NOISE_PERIOD_ON_OFF
		;; NO, BIT5 IS NOT SET - VELOCITY IS NOT TONE A @ NIOSE PERIOD
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_CHK_VELOCITY_NOISE_CH_B
	
		;; YES - BIT5 IS SET, SO IT IS TONE A @ NOISE PERIOD
		;; SWITCH TO TONE B - TONE B MUST BE VELOCITY FOR NOISE PERIOD 
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_B

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_B
	;; SET VELOCITY TONE B TO NOISE PERIOD
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT5_VELOCITY_A_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH OFF VELOCITY TONE A
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT6_VELOCITY_B_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH ON  VELOCITY TONE B
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT7_VELOCITY_C_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH OFF VELOCITY TONE C
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_CHK_VELOCITY_NOISE_CH_B
	;; CHECK IF BIT6 [TONE B] is SET
	btfss AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, 6
		;; NO, BIT6 IS NOT SET - ITS MUST BE TONE C 
		;; MUST BE BIT 7 - TONE C 
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_VELOCITY_IS_NOISE_CH_C
	
		;; YES - BIT6 IS SET, SO IT IS TONE B @ NOISE PERIOD
		;; TONE C MUST BE VELOCITY FOR NOISE PERIOD 
		rgoto LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_C

LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_SET_VELOCITY_TO_NOISE_CH_C 
	;; SET VELOCITY TONE C TO NOISE PERIOD
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT5_VELOCITY_A_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH OFF VELOCITY TONE A
	bcf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT6_VELOCITY_B_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH OFF VELOCITY TONE B
	bsf	AY_VELOCITY_TO_ABC_NOISE_PERIOD_ON_OFF, BIT7_VELOCITY_C_TO_NOISE_PERIOD_ON_OFF  ;; SWITCH ON  VELOCITY TONE C
		;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH


LABEL_AY_SUB_MENU_PLUS_MAIN_MENU7_VELOCITY_IS_NOISE_CH_C
	      ;; VELOCITY IS @ TONE C 
	      ;; NOTHING MORE TO DO HERE
	      ;; WORK IS DONE BACK TO MAIN MENU
		goto LABEL_MAIN_MENUE_SWITCH

